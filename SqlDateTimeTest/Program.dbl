
import SynPSG.DatabaseTools

main
    ;.define D_DatabaseType      DatabaseType.SQLServer
    .define D_DatabaseType      DatabaseType.MySQL8

    .define D_ConnectionType    ConnectionType.Local
    ;.define D_ConnectionType    ConnectionType.Remote

    .define D_DatabaseName      "synergytestdb"
    .define D_DatabaseUser      "syntest"
    .define D_DatabasePassword  "syntest"

    .define D_DatabaseServer    "localhost"
    .define D_DatabasePort      1958

    ; If a program needs to access date and time fields, the only way to do that with SQL Connection
    ; is to format all date and time fields as full datetime values, vis temporary variables, then
    ; move the data out of the temporary variables into the date and time fields in your records.
    ; This structure is a useful way to define the datetime fields in such a way that it is easy to
    ; access the required underlying D8 or D6 date values, and D6 or D4 times values.
    structure strDateTime
        group datetime,     d14
            group date8,    d8
                year,       d4
                month,      d2
                day,        d2
            endgroup
            group date6,    d6, X
                year,       d4
                month,      d2
            endgroup
            group time6,    d6
                hour,       d2
                minute,     d2
                second,     d2
            endgroup
            group time4,    d4, X
                hour,       d2
                minute,     d2
            endgroup
        endgroup
    endstructure

    .include "CONNECTDIR:ssql.def"
    record
        ok,                 boolean, true
        tt,                 i4
        connected,          boolean
        cursorOpen,         boolean
        doCreateDatabase,   boolean
        databaseCreated,    boolean
        db,                 i4
        cr,                 i4
        status,             i4
        sql,                string
        ; Declare temp DateTime storage for any date or time fields to be retrieved
        tmpEmpHireDate,     strDateTime
        tmpEmpHireTime,     strDateTime
        tmpEmpDateOfBirth,  strDateTime
    endrecord

    record employee
        emp_id,             d6
        emp_first_name,     a20
        emp_last_name,      a20
        emp_dept,           a15
        emp_hire_date,      d8
        emp_hire_time,      d4
        emp_phone_work,     a14
        emp_phone_cell,     a14
        emp_paid,           d1
        group emp_birth,    d
            date,           d8
            time,           d4
        endgroup
        emp_email,          a40
        emp_address_street, a30
        emp_address_city,   a20
        emp_address_state,  a2
        emp_address_zip,    d5
    endrecord

proc
    ; Configure the environment

    open(tt=0,i,"tt:")
    xcall flags(7004020,1)


    ; Enable SQL Connection

    .ifdef OS_VMS
    xcall init_ssql
    .else
    status = %option(48,1)
    .endc

    ; Initialize SQL Connection

    ok = (%ssc_init(db=1) == SSQL_NORMAL)

    ; Connect and log in to MySQL

    if (ok)
    begin
        data connectString = Db.GetConnectString(D_DatabaseType,D_ConnectionType,D_DatabaseUser,D_DatabasePassword,D_DatabaseName,D_DatabaseServer,D_DatabasePort)

        if (%ssc_connect(db=1,connectString) == SSQL_NORMAL) then
        begin
            set ok, connected = true
            writes(tt,"Connected and logged in")
        end
        else
            Db.LogDatabaseError(db,tt,"Failed to connect to database!")
    end

    ; Configure date format to YYYYMMDDHHMMSS

    if (ok && connected)
    begin
        ; By default SQL Connection returns DATE, TIME and DATETIME fields as DD-MON-YYYY HH:MI:SS. E.g "25-DEC-2020 09:30:00"
        ; So by default you must use an A20 field for MySQL DATE fields.
        ; This command causes DATE, TIME and DATETIME fields to be returned as a D14 in YYYYMMDDHHMISS format.
        ; You MUST also cast the decimal variable to ALPHA with ^A when data binding with %SSC_DEFINE. (see EMP_HIRE_DATE in this code)
        ; OR .. overlay the D8 with an A8 and use the A8 for binding (see EMP_DATE_OF_BIRTH in this code)
        ;        xcall ssc_option(db,SSQL_SETOPT,-1721378,"YYYYMMDD",0)
        xcall ssc_option(db,SSQL_SETOPT,-1721378,"YYYYMMDDHHMISS",0)
    end

    ; Select and display an employee record

    if (ok && connected)
    begin
        ; Define the SQL statement to execute
        sql = "SELECT * FROM employee WHERE EmpId=1"

        ; Open cursor
        if (%ssc_open(db,cr,sql,SSQL_SELECT) == SSQL_NORMAL) then
        begin
            cursorOpen = true

            ; Define host variables
            if (%ssc_define(db,cr,15,
            &    emp_id,
            &    emp_first_name,
            &    emp_last_name,
            &    emp_dept,
            &    tmpEmpHireDate,
            &    tmpEmpHireTime,
            &    emp_phone_work,
            &    emp_phone_cell,
            &    emp_paid,
            &    tmpEmpDateOfBirth,
            &    emp_email,
            &    emp_address_street,
            &    emp_address_city,
            &    emp_address_state,
            &    emp_address_zip
            &    ) == SSQL_NORMAL) then
            begin
                ; Execute cursor
                if (%ssc_move(db,cr) == SSQL_NORMAL) then
                begin
                    writes(tt,"Row retrieved")

                    ; Move the date and time fields from temp storage into the record
                    emp_hire_date = tmpEmpHireDate.date8
                    emp_hire_time = tmpEmpHireTime.time4
                    emp_birth.date = tmpEmpDateOfBirth.date8
                    emp_birth.time = tmpEmpDateOfBirth.time4

                    call PrintEmployee

                    ; Close cursor
                    if (%ssc_close(db,cr) != SSQL_NORMAL)
                    begin
                        Db.LogDatabaseError(db,tt,"Failed to close cursor!")
                    end
                end
                else
                begin
                    Db.LogDatabaseError(db,tt,"Failed to SELECT row!")
                end
            end
            else
            begin
                Db.LogDatabaseError(db,tt,"Failed to define host variables!")
            end
        end
        else
        begin
            Db.LogDatabaseError(db,tt,"Failed to open cursor!")
        end

    end

    ; Disconnect from the database

    if (connected)
    begin
        connected = %ssc_release(db)
        writes(tt,"Connection closed")
    end

    ; Disable SQL Connection

    .ifndef OS_VMS
    status = %option(48,0)
    .endc

    ; All done

    begin
        data achar, a1
        display(tt,"Press a key: ")
        accept(tt,achar)
    end

    close tt

    stop

PrintEmployee,
    writes(tt," - Employee ID               : " + ^a(EMP_ID))
    writes(tt," - Hire date          (DATE) : " + %string(emp_hire_date,"XXXX-XX-XX"))
    writes(tt," - Hire time          (TIME) : " + %string(emp_hire_time,"XX:XX"))
    writes(tt," - Date of birth  (DATETIME) : " + %string(emp_birth.date,"XXXX-XX-XX"))
    writes(tt," - Time of birth  (DATETIME) : " + %string(emp_birth.time,"XX:XX"))
    return

endmain